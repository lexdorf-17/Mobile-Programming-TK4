apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 34
    buildToolsVersion "33.0.0"

    defaultConfig {
        applicationId "com.example.binusapp"
        minSdkVersion 23
        targetSdkVersion 34
        versionCode 1
        versionName "1.0.0"
        multiDexEnabled true
        //vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    // This is handled for you by the 2.0+ Gradle Plugin
    aaptOptions {
        additionalParameters "--no-version-vectors"
    }

    signingConfigs {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        def keyAliasFix = properties.getProperty('KEY_ALIAS')
        def keyPasswordFix = properties.getProperty('KEY_PASSWORD')
        release {
            storeFile file('binusapp.keystore')
            keyAlias keyAliasFix
            keyPassword keyPasswordFix
            storePassword keyPasswordFix
        }

        debug {
            storeFile file('binusapp.keystore')
            keyAlias keyAliasFix
            keyPassword keyPasswordFix
            storePassword keyPasswordFix
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.all {
                    def versionName = variant.versionName
                    def formattedDate = new Date().format('dd-MMMM-yyyy_HH-mm')
                    outputFileName = "BinusAppV${versionName}_${formattedDate}.apk"
                }
            }
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    sourceSets {
        main {
            res.srcDirs =
                    [
                            file("src/main/res/layouts/launch/").listFiles(),
                            file("src/main/res/layouts/login/").listFiles(),
                            file("src/main/res/layouts/main/").listFiles(),
                            file("src/main/res/layouts/").listFiles(),
                            'src/main/res/layouts',
                            'src/main/res'
                    ]
        }
    }

    compileOptions {
        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }

    tasks.withType(Test) {
        testLogging {
            exceptionFormat "full"
            events "started", "skipped", "passed", "failed"
            showStandardStreams true
        }
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    packagingOptions {
        resources.excludes.add("META-INF/*")
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // permission
    implementation 'io.github.inflationx:calligraphy3:3.1.1'
    implementation 'io.github.inflationx:viewpump:2.0.3'

    implementation 'com.google.firebase:firebase-auth:21.0.4'
    implementation 'com.google.android.gms:play-services-auth:20.2.0'

    // maps
    implementation 'com.google.android.gms:play-services-maps:18.2.0'
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    implementation 'com.google.android.gms:play-services-basement:18.3.0'
    implementation 'com.google.android.gms:play-services-places:17.0.0'
    implementation 'com.google.maps.android:android-maps-utils:0.5'

    //secure pref
    implementation "androidx.security:security-crypto:1.0.0"

    //unit test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}